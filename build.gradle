plugins {
    id "java"
    id "org.springframework.boot" version "3.2.5"
    id "io.spring.dependency-management" version "1.1.4"
    id "org.flywaydb.flyway" version "9.22.3"
    id "checkstyle"
}

group = "com.eviive.personalapi"
version = "1.2.3"

java {
    sourceCompatibility = "17"
}

flyway {
    user = flyway.user
    password = flyway.password
    url = flyway.url
    schemas = ["public"]
    locations = ["classpath:db/migration"]
    cleanDisabled = flyway.cleanDisabled
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set("jwtVersion", "4.4.0")
    set("mapStructVersion", "1.5.5.Final")
    set("swaggerVersion", "2.5.0")
    set("springCloudAzureVersion", "5.11.0")
}

dependencies {
    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"

    // Auth0 JWT
    implementation "com.auth0:java-jwt:${jwtVersion}"

    // Azure
    implementation "com.azure.spring:spring-cloud-azure-starter-actuator"
    implementation "com.azure.spring:spring-cloud-azure-starter-storage"

    // Lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // PostgreSQL
    runtimeOnly "org.postgresql:postgresql"

    // Flyway
    implementation "org.flywaydb:flyway-core"

    // MapStruct
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    // Swagger
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${swaggerVersion}"
}

dependencyManagement {
    imports {
        mavenBom "com.azure.spring:spring-cloud-azure-dependencies:${springCloudAzureVersion}"
    }
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("**/application*.properties") {
        expand(project.properties)
    }
}

checkstyle {
    toolVersion "10.15.0"
}
